// Capuche cleaning Base on the poring race by Zell_ff8, xianz, Neko, for fun

prontera,146,205,3	script	Corrida Poring	91,{
cutin "katsua01",2;
	if( .access_Prace == 0 ) {
		mes "[Corrida Poring]";
		mes "A corrida já terminou.";
		mes "Espera até a próxima vez para participar.";
		mes "Horário de abertura a cada 1 hora.";
		mes "Iniciando em 10:20, 11:20, 12:20...";
		close2;
		cutin "",255;
		end;
	}
	mes "[Corrida Poring]";
	mes "Gostaria de participar da Corrida Poring?";
	if( select( "Sim","Não" ) -1 ) {
		next;
		mes "[Corrida Poring]";
		mes "Ok, vejo você em breve!";
		close;
	}
	if( .access_Prace == 0 ) {
		next;
		mes "[Corrida Poring]";
		mes "...";
		mes "...";
		mes "Cheater!!~~";
		close;
	}
	close2;
	@prace_winner$ = "";
	warp "p_track01",52,41;
	end;
// OnInit:
OnMinute20:
	if( gettime(3)%2 ) end;
	set .access_Prace, 1;
	announce "A Corrida Poring está aberta!",bc_all,0x00FFFF;
	setnpctimer 100000,"Licitante#prace0";
	startnpctimer "Licitante#prace0";
	end;
OnPraceEnd:
	set .access_Prace, 0;
	announce "A Corrida Poring fechou!",bc_all,0x00FFFF;
	end;
OnInit:
	waitingroom "Corrida Poring",0;
	end;
}


p_track01,58,41,3	script	Licitante#prace0	877,{
function checkevent;

	mes "[Licitante]";
	if( getstrlen( @prace_winner$ ) ) {
		mes "Você escolheu ^00bb00"+ @prace_winner$ +"^000000";
		close;
	}
	else if ( checkevent() || .start ) {
		mes "A corrida está em andamento...";
		close;
	}
	else if( !getvariableofnpc( .access_Prace,"Corrida Poring" ) ) {
		mes "A corrida ainda não começou.";
		close;
	}
	mes "Faça sua aposta:";
//	mes "Irá custar "+ .zeny_req +" zenys.";

	.@s = select( .menu_$ );
	if( .@s == 7 ) {
		next;
		mes "[Licitante]";
		mes "Adeus.";
		close;
	}
	.@c$ = .monst_$[ .@s -1 ];

	if ( checkevent() ) {
		next;
		mes "[Licitante]";
		mes "...";
		mes "...";
		mes "Cheater!!~~";
		close;
	}
	else if( Zeny < .zeny_req ) {
		next;
		mes "[Licitante]";
		mes "Você não tem essa quantia em zeny.";
		close;
	}
	Zeny -= .zeny_req;
	@prace_winner$ = .@c$;
	.prace_bidders[ .prace_bets ] = getcharid(3);
	.prace_bets++;

	next;
	mes "[Licitante]";
	mes "Eu tenho "+ .prace_bets +" apostas.";
	initnpctimer;
	npctalk ""+ strcharinfo(0) +" fez a sua aposta!";
	close;
OnTimer60000:
	npctalk "Eu tenho "+ .prace_bets +" apostas. Alguém mais?";
	end;
OnTimer80000:
	npctalk "A corrida vai começar. Última chance.";
	end;
OnTimer90000:
	stopnpctimer;
	.start = 1;
	mapannounce "p_track01","Porings, em suas marcas...",1,0xFFAB54;
	sleep 2500;
	for( .@i = 3; .@i > 0; .@i-- ) {
		mapannounce "p_track01","..."+ .@i +"...",1,0xFFAB54;
		sleep 1000;
	}
	donpcevent strnpcinfo(0) +"::OnStartRace";
	sleep 1000;
	mapannounce "p_track01","Vaaaiii!!!",1,0xFFAB54;
	end;
OnTimer320000:
	mapwarp "p_track01","prontera",142,170;
	donpcevent "Corrida Poring::OnPraceEnd";
	.prace_winner$ = "";
	.start = .prace_bets = 0;
	donpcevent strnpcinfo(0) +"::OnReturnRace";
	end;

OnStartRace:
	callsub L_label, "OnRace";
OnStopRace:
	callsub L_label, "OnStop";
OnReturnRace:
	callsub L_label, "OnReturn";
L_label:
	donpcevent "Metaling#prace3::"+ getarg(0);
	donpcevent "Poring#prace1::"+ getarg(0);
	donpcevent "Poporing#prace6::"+ getarg(0);
	donpcevent "Angeling#prace2::"+ getarg(0);
	donpcevent "Poring Noel#prace5::"+ getarg(0);
	donpcevent "Deviling#prace4::"+ getarg(0);
	if( getarg(0) == "OnStop" && .prace_winner$ != "" )
		callsub L_WinRace;
	end;
L_WinRace:
	mapannounce "p_track01", "O vencedor é "+ .prace_winner$,1,0xFFAB54;
	donpcevent strnpcinfo(0) +"::OnChequeo";
	sleep 3000;
	donpcevent strnpcinfo(0) +"::OnReturnRace";
	sleep 10000;
	mapwarp "p_track01","prontera",142,170;
	donpcevent "Corrida Poring::OnPraceEnd";
	.prace_winner$ = "";
	.start = .prace_bets = 0;
	end;
OnChequeo:
	for( .@i = 0 ; .@i < getarraysize( .prace_bidders ); .@i++ ) {
		if( attachrid( .prace_bidders[.@i] ) && getstrlen( @prace_winner$ ) ) {
			dispbottom "O vencedor é "+ .prace_winner$ +" e você apostou em "+ @prace_winner$ +".";
			if( @prace_winner$ == .prace_winner$ ) {
				dispbottom "Você venceu!";
				mapannounce "p_track01"," Parabéns! "+ strcharinfo(0) +" venceu!",1,0xFFAB54;
				getitem .item_gained, .item_num_gain;
				emotion 21,1;
			}
			else {
				dispbottom "Você perdeu.";
				emotion 28,1;
			}
			@prace_winner$ = "";
		}
	}
	deletearray .prace_bidders;
	end;
OnInit:
	.zeny_req = 0;
	.item_gained = 29000;
	.item_num_gain = 1;
	setarray .monst_$,"Poring","Angeling","Metaling","Deviling","Poring Noel","Poporing","Nenhum";
	.menu_$ = implode( .monst_$, ":" );
	end;

function checkevent {
	getmapxy .@mapname$, .@x1, .@y, 1, "Poring#prace1";
	getmapxy .@mapname$, .@x2, .@y, 1, "Angeling#prace2";
	getmapxy .@mapname$, .@x3, .@y, 1, "Metaling#prace3";
	getmapxy .@mapname$, .@x4, .@y, 1, "Deviling#prace4";
	getmapxy .@mapname$, .@x5, .@y, 1, "Poring Noel#prace5";
	getmapxy .@mapname$, .@x6, .@y, 1, "Poporing#prace6";
	.@t = ( .@x1 + .@x2 + .@x3 + .@x4 + .@x5 + .@x6 ) != 58 * 6;
	return .@t;
}
}
//-----------------------------------
// Racer NPC's
//-----------------------------------

-	script	pori_race	-1,{
OnRace:
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
OnReturn:
	sleep 1000;
	while( strnpcinfo(1) != .monst$[ .@i ] ) .@i++;
	movenpc strnpcinfo(3), 58, .walk_t[.@i];
	end;
OnTimer1100:
	getmapxy .@mapname$,.@x,.@y,1, strnpcinfo(3);
	if( rand(100) < .prace_random )
		npcwalkto .@x-1, .@y;
	.@r = rand( .prace_random2 );
	if ( .@x -1 == 29 ) {
		while( strnpcinfo(1) != .monst$[ .@i ] ) .@i++;
		set getvariableofnpc( .prace_winner$, "Licitante#prace0" ), .monst$[ .@i ];
		emotion 29;
		donpcevent "Licitante#prace0::OnStopRace";
		end;
	}
	stopnpctimer;
	setnpctimer .@r;
	startnpctimer;
	end;
OnInit:
	deletearray .walk_t;
	deletearray .monst$;
	setarray .walk_t, 38, 36, 34, 32, 30, 28;
	setarray .monst$, "Poring", "Angeling", "Metaling", "Deviling", "Poring Noel", "Poporing";
	.prace_random = 70;
	.prace_random2 = 600;
	end;
}

p_track01,58,38,2	duplicate(pori_race)	Poring#prace1	1002
p_track01,58,36,2	duplicate(pori_race)	Angeling#prace2	1096
p_track01,58,34,2	duplicate(pori_race)	Metaling#prace3	1613
p_track01,58,32,2	duplicate(pori_race)	Deviling#prace4	1582
p_track01,58,30,2	duplicate(pori_race)	Poring Noel#prace5	1062
p_track01,58,28,2	duplicate(pori_race)	Poporing#prace6	1031

p_track01,78,42,0	warp	p_track002	1,3,prontera,142,170

p_track01	mapflag	nobranch
p_track01	mapflag	noicewall
p_track01	mapflag	nomemo
p_track01	mapflag	noreturn
p_track01	mapflag	noteleport
p_track01	mapflag	nowarpto
p_track01	mapflag	nowarp
p_track01	mapflag	pvp	off
p_track01	mapflag	nosave