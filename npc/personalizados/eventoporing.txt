//|==============================================================================================|\\
//|         **************************  Evento Caçada Poring  **************************         |\\
//|==============================================================================================|\\
prontera,130,215,6	script	Caçada Poring	508,{
	if(gettime(2)>=35)
		set .minutos_abrir,35-gettime(2)+60;//Seta variável com minutos que falta para abrir o evento
	if(gettime(2)<35)
		set .minutos_abrir,35-gettime(2);//No caso é todo minuto 35 que abre o evento
	mes .npc$;
	mes "Bem vind"+(Sex?"o":"a")+" ao evento ^FF9A00"+.nome_evento$+"^000000!";
	if(.eventoaberto!=1){
		mes "^FF0000Evento fechado^000000";
		mes "Abrirá em: ^FF0000"+.minutos_abrir+"^000000 minutos.";
	} else {
		mes "^00AA00Evento aberto!^000000";
	}
	mes "O que deseja fazer?";
	mes "Você tem ^0000FF"+Ponto_Cacada_Poring+"^000000 pontos!";
	if(getgmlevel()>=.lvlgm)
		mes "Pode usar o comando ^00AA00@"+.nome_evento$+"^000000 para manipular o evento";
	if(getgmlevel()==99)//Somente o ADM tem essa opção
		set .@m$,"Manipular Evento";
	next;
	switch(select("Entrar no Evento","Trocar pontos","Informações",.@m$))
	{
		mes .npc$;
		case 1:
			if(.eventoaberto == 1){
				mes "^ff6600Boa sorte!^000000";
				next;
				warp .mapa$,0,0; 
				end;
			} else {
				mes "^ff6600O evento está fechado!^000000";
			}
			close;
		case 2:
			mes "Você deseja troca seus pontos por algum destes itens?";
			mes "^6495ED"+getitemname($Item1)+"^000000 = "+$Pontos1+" Pontos";
			mes "^6495ED"+getitemname($Item2)+"^000000 = "+$Pontos2+" Pontos";
			mes "^6495ED"+getitemname($Item3)+"^000000 = "+$Pontos3+" Pontos";
			next;
			if(select("Sim","Não")==2) close;
			mes .npc$;
			mes "Esolha o que você deseja.";
			next;
			switch(select(getitemname($Item1),getitemname($Item2),getitemname($Item3)))
			{
				mes .npc$;
				case 1:
					mes "Digite a quantidade de ^6495ED"+getitemname($Item1)+"^000000 que você deseja.";
					input @Quantia;
					if (@Quantia==0) close;
					if(Ponto_Cacada_Poring < $Pontos1*@Quantia){
						next;
						mes .npc$;
						mes "Você não tem pontos para retirar essa quantia de ^6495ED"+getitemname($Item1)+"^000000.";
						close;
					}
					next;
					mes .npc$;
					mes "Muito bem aqui está seu premio!";
					set Ponto_Cacada_Poring,Ponto_Cacada_Poring-$Pontos1*@Quantia;
					getitem $Item1,@Quantia;
					close;
				case 2:
					mes "Digite a quantidade de ^6495ED"+getitemname($Item2)+"^000000 que você deseja.";
					input @Quantia;
					if (@Quantia==0) close;
					if(Ponto_Cacada_Poring < $Pontos2*@Quantia){
						next;
						mes .npc$;
						mes "Você não tem pontos para retirar essa quantia de ^6495ED"+getitemname($Item2)+"^000000.";
						close;
					}
					next;
					mes .npc$;
					mes "Muito bem aqui está seu premio!";
					set Ponto_Cacada_Poring,Ponto_Cacada_Poring-$Pontos2*@Quantia;
					getitem $Item2,@Quantia;
					close;
				case 3:
					mes "Digite a quantidade de ^6495ED"+getitemname($Item3)+"^000000 que você deseja.";
					input @Quantia;
					if (@Quantia==0) close;
					if(Ponto_Cacada_Poring < $Pontos3*@Quantia){
						next;
						mes .npc$;
						mes "Você não tem pontos para retirar essa quantia de ^6495ED"+getitemname($Item3)+"^000000.";
						close;
					}
					next;
					mes .npc$;
					mes "Muito bem aqui está seu premio!";
					set Ponto_Cacada_Poring,Ponto_Cacada_Poring-$Pontos3*@Quantia;
					getitem $Item3,@Quantia;
					close;
			}
		case 3:
			mes "O evento funciona da seguinte forma:";
			next;
			mes .npc$;
			mes "Os jogadores são enviados em uma cordenada aleatória do mapa.";
			mes "Em seguida é começado o 1º round.";
			mes "Os jogadores precisam matar todos os porings para avançar para o próximo round.";
			next;
			mes .npc$;
			mes "Cada poring da sorte vale uma quantidade de pontos de acordo com seu tipo:";
			next;
			mes .npc$;
			mes "Poring ^FF8DDCROSA^000000= 1 ponto";
			mes "Poring ^FFAB00LARANJA^000000= 2 pontos";
			mes "Poring ^1DC400VERDE^000000= 3 pontos";
			mes "Poring ^0000FFAZUL^000000= 4 pontos";
			mes "Poring ^9500FFROXO^000000= 5 pontos";
			mes "Poring ^944C00TERRA^000000= 10 pontos";
			mes "Poring ^EB2B00FOGO^000000= 20 pontos";
			mes "Poring ^00CDFFAGUA^000000= 30 pontos.";
			mes "Poring ^B4BAFFANJO^000000= 50 pontos.";
			mes "Poring ^909090PRINCIPE^000000= 100 pontos.";
			mes "	^000000REI^000000= 1000 pontos.";
			next;
			mes .npc$;
			mes "No 3º e último round é invocado o Rei Poring que vale 1000 pontos.";
			mes "Após o Rei Poring morrer ou 10 minutos se passarem desde o inicio do 1º round o evento é finalizado.";
			next;
			mes .npc$;
			mes "Os pontos adquiridos no evento podem ser trocados por prêmios no próprio NPC Caçada Poring.";
			close;
		case 4:
			mes "O que deseja fazer ^FF4500"+strcharinfo(0)+"^000000?";
			mes "Os premios são:";
			mes "^6495ED"+getitemname($Item1)+"^000000 = "+$Pontos1+" Pontos";
			mes "^6495ED"+getitemname($Item2)+"^000000 = "+$Pontos2+" Pontos";
			mes "^6495ED"+getitemname($Item3)+"^000000 = "+$Pontos3+" Pontos";
			next;
			switch(select("Mudar Premio","Abrir Evento","Finalizar Evento"))
			{
				mes .npc$;
				case 1:
					mes "Informe o ID do primeiro item para ser o Prêmio.";
					input $Item1;
					next;
					mes .npc$;
					mes "Informe a quantidade de pontos para trocar pelo item.";
					input $Pontos1;
					next;
					mes .npc$;
					mes "Informe o ID do segundo item para ser o Prêmio.";
					input $Item2;
					next;
					mes .npc$;
					mes "Informe a quantidade de pontos para trocar pelo item.";
					input $Pontos2;
					next;
					mes .npc$;
					mes "Informe o ID do terceiro item para ser o Prêmio.";
					input $Item3;
					next;
					mes .npc$;
					mes "Informe a quantidade de pontos para trocar pelo item.";
					input $Pontos3;
					mes "Feito.";
					close;
				case 2:
					mes "O evento será iniciado.";
					close2;
					dispbottom "["+.nome_evento$+"] Evento aberto";
					goto OnAbrirEvento;
				case 3:
					mes "O evento será finalizado.";
					close2;
					announce "O Evento ["+.nome_evento$+"] finalizou!",bc_all,0x00FFFF;
					dispbottom "["+.nome_evento$+"] Evento finalizado";
					goto FinalizarEvento;
			}
	}
	
	//======================SETAR PRINCIPAIS VARIÁVEIS======================
	OnInit:
		set .npc$,"[^0000ffCaçada Poring^000000]";//=================Nome do NPC que aparece nas mensagens
		set .mapa$,"moonhaven";//====================================Mapa do evento
		set .lvlgm,99;//=============================================Nível de GM necessário para manipular evento
		set .nome_evento$,"Caçada Poring";//=========================Nome do evento
		set .eventoaberto,0;//=======================================Inicia o npc com o evento fechado
		set .eventoiniciado,0;//=====================================Inicia o npc com o evento não iniciado
		waitingroom .nome_evento$,0;//===============================Cria sala no npc com nome do evento
		bindatcmd "poring",strnpcinfo(3)+"::OnAtcommand";//==========Cria comando para GM manipular evento
		removemapflag .mapa$,mf_pvp;//===============================Desabilita PvP no mapa ao iniciar o npc
		end;

	OnAtcommand:
		if(getgroupid()>=lvlgm){
			mes .npc$;
			mes "O que deseja fazer ^FF4500"+strcharinfo(0)+"^000000?";
			switch (select("Abrir Evento","Finalizar Evento","Próximo Round","Iniciar Agora!"))
			{
				case 1:
					close2;
					dispbottom "["+.nome_evento$+"] Evento aberto";
					goto OnAbrirEvento;
					end;
				case 2:
					close2;
					announce "O Evento ["+.nome_evento$+"] finalizou!",bc_all,0x00FFFF;
					dispbottom "["+.nome_evento$+"] Evento finalizado";
					goto FinalizarEvento;
					end;
				case 3:
					close2;
					dispbottom "["+.nome_evento$+"] Precisa estar no 1º ou 2º round.";
					if (.round==1||.round==2) {
						set .mob,1;
						goto MobMorreu;
					}
					end;
				case 4:
					close2;
					dispbottom "["+.nome_evento$+"] O evento precisa estar aberto.";
					if (.eventoaberto==1) {
						setnpctimer 119999,"Caçada Poring";
					}
					end;
			}
		}
		end;

	OnMinute35:
		if((.eventoaberto==1)||(.eventoiniciado==1)){//Verificar se já foi aberto ou iniciado
			end;
		} else {
			goto OnAbrirEvento;
		}
		end;

	OnAbrirEvento:
		set .eventoaberto,1;//Abre evento, setando variável permitindo entrar no evento
		mapwarp .mapa$,"prontera",156,182;
		killmonsterall .mapa$;
		announce "O Evento ["+.nome_evento$+"] está aberto, você tem 2 minutos para entrar!",bc_all,0x00FFFF;
		
		set .round,0;
		set .mob,0;
		initnpctimer;
		end;
		
		sleep 120000;//usar somente 1 sleep
		announce "O Evento ["+.nome_evento$+"] fechou!",bc_all,0x00FFFF;
		goto OnIniciarEvento;
		end;

	OnTimer60000:
		announce "O Evento ["+.nome_evento$+"] vai iniciar em 1 minuto!",bc_all,0x00FFFF;
		end;

	OnTimer120000:
		set .eventoaberto,0;
		announce "O Evento ["+.nome_evento$+"] fechou!",bc_all,0x00FFFF;
		set .round,1;
		goto ProximoRound;

	OnTimer660000:
		mapannounce .mapa$,"A caçada vai terminar em 1 minuto.",bc_all,0xAAFF00;
		end;

	OnTimer720000:
		if (.round==3) {
			mapannounce .mapa$,"O tempo acabou! O Rei Poring sobreviveu...",bc_all,0xAAFF00;
		} else {
			mapannounce .mapa$,"O tempo acabou! Restaram "+.mob+" porings para o próximo round",bc_all,0xAAFF00;
		}
		goto FinalizarEvento;

	ProximoRound:
		if (.round==1) {
			mapannounce .mapa$,"1º round começou!",bc_all,0xAAFF00;
			set .mob,510;
			monster .mapa$,0,0,"Poring Rosa",2909,200,""+strnpcinfo(1)+"::OnMorreuRosa";
			monster .mapa$,0,0,"Poring Laranja",2911,150,""+strnpcinfo(1)+"::OnMorreuLaranja";
			monster .mapa$,0,0,"Poring Verde",2910,100,""+strnpcinfo(1)+"::OnMorreuVerde";
			monster .mapa$,0,0,"Poring Azul",2912,50,""+strnpcinfo(1)+"::OnMorreuAzul";
			monster .mapa$,0,0,"Poring Roxo",2904,10,""+strnpcinfo(1)+"::OnMorreuRoxo";
		}
		if (.round==2) {
			set .mob,68;
			mapannounce .mapa$,"2º round começou!",bc_all,0xAAFF00;
			monster .mapa$,0,0,"Poring da Terra",2907,50,""+strnpcinfo(1)+"::OnMorreuTerra";
			monster .mapa$,0,0,"Poring do Fogo",2906,10,""+strnpcinfo(1)+"::OnMorreuFogo";
			monster .mapa$,0,0,"Poring da Agua",2903,5,""+strnpcinfo(1)+"::OnMorreuAgua";
			monster .mapa$,0,0,"Poring Anjo",2905,2,""+strnpcinfo(1)+"::OnMorreuAnjo";
			monster .mapa$,0,0,"Principe Poring",2908,1,""+strnpcinfo(1)+"::OnMorreuPrincipe";
		}
		if (.round==3) {
			set .mob,1;
			mapannounce .mapa$,"3º round começou!",bc_all,0xAAFF00;
			monster .mapa$,0,0,"Rei Poring",2902,1,""+strnpcinfo(1)+"::OnRei";
		}
		end;

	OnMorreuRosa:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+1;
		goto MobMorreu;

	OnMorreuLaranja:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+2;
		goto MobMorreu;

	OnMorreuVerde:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+3;
		goto MobMorreu;

	OnMorreuAzul:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+4;
		goto MobMorreu;

	OnMorreuRoxo:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+5;
		goto MobMorreu;

	OnMorreuTerra:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+10;
		goto MobMorreu;

	OnMorreuFogo:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+20;
		goto MobMorreu;

	OnMorreuAgua:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+30;
		goto MobMorreu;

	OnMorreuAnjo:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+50;
		goto MobMorreu;

	OnMorreuPrincipe:
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+100;
		goto MobMorreu;

	OnRei:
		specialeffect2 381;
		set Ponto_Cacada_Poring,Ponto_Cacada_Poring+1000;
		set @randcoin, rand(50,100);
		set #CASHPOINTS,#CASHPOINTS+@randcoin;
		announce "["+.nome_evento$+"] O Rei Poring foi morto por "+strcharinfo(0)+"! Sua recompensa é: 1000 pontos + "+@randcoin+" Coins!",bc_all,0xFF70FF;
//		mapannounce .mapa$,"Parabéns "+strcharinfo(0)+"! você exterminou o Rei Poring!",bc_all,0xAAFF00;
//		dispbottom "Sua recompensa é: 1000 pontos + ^ff0000"+@randcoin+"^000000 Coins!";
		sleep 3000;
		goto MobMorreu;

	MobMorreu:
		set .mob,.mob-1;
		if(.mob == 200) mapannounce .mapa$,"Faltam 200 porings",bc_all,0xAAFF00;
		if(.mob == 50) mapannounce .mapa$,"Faltam 50 porings",bc_all,0xAAFF00;
		if(.mob == 10) mapannounce .mapa$,"Faltam 10 porings",bc_all,0xAAFF00;
		if(.mob == 1) mapannounce .mapa$,"Falta 1 poring",bc_all,0xAAFF00;
		if(.mob == 0) {
			mapannounce .mapa$,"Todos os porings foram mortos.",bc_all,0xAAFF00;
			killmonsterall .mapa$;
			sleep 3000;
			if (.round==1) {
				set .round,2;
				goto ProximoRound;
			}
			if (.round==2) {
				set .round,3;
				goto ProximoRound;
			}
			if (.round==3) {
				mapannounce .mapa$,"A caçada poring terminou...",bc_all,0xAAFF00;
				sleep 3000;
				goto FinalizarEvento;
			}
		}
		end;

	FinalizarEvento:
		mapwarp .mapa$,"prontera",156,182;
		sleep 1000;
		killmonsterall .mapa$;
		set .round,0;
		set .eventoaberto,0;
		set .mob,0;
		initnpctimer;
		stopnpctimer;
		end;

	end;
}
// Map-Flags
moonhaven	mapflag	nowarp
moonhaven	mapflag	nowarpto
moonhaven	mapflag	noteleport
moonhaven	mapflag	nosave	SavePoint
moonhaven	mapflag	nomemo
moonhaven	mapflag	nobranch
moonhaven	mapflag	nopenalty
moonhaven	mapflag	monster_noteleport
moonhaven	mapflag	noreturn
moonhaven	mapflag	noskill